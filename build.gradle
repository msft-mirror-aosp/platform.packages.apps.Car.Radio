/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'buildSrc.jdk.jdk8'
}

android {
    namespace "com.android.car.radio"
    compileSdkVersion gradle.ext.aaosLatestSDK
    defaultConfig {
        applicationId "com.android.car.radio"
        minSdkVersion 33
        targetSdkVersion 33
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java.srcDirs = ['src']
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
        }
    }

    signingConfigs {
        aaos {
            storeFile file(gradle.ext.aaosCertPath)
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }

        platform {
            storeFile file(gradle.ext.platformCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    flavorDimensions "signingkey"
    productFlavors {
        aaos {
            dimension "signingkey"
            signingConfig signingConfigs.aaos
        }
        platform {
            dimension "signingkey"
            if (file(gradle.ext.platformCertPath).exists()) {
                signingConfig signingConfigs.platform
            }
        }
    }

    buildTypes {
        debug {
            signingConfig android.buildTypes.release.signingConfig
        }
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    implementation files(gradle.ext.lib_car_system_stubs)

    implementation(project(":car-broadcastradio-support"))
    implementation(project(":car-apps-common"))
    implementation(project(":car-media-common"))
    implementation(project(":car-ui-lib"))
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation "androidx.media:media:1.6.0"

    def room_version = '2.4.0-alpha03'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // Not available in 2.3+
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
}
